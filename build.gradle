buildscript {
    ext {
        assetPipelineVersion = '2.11.6'
        grailsVersion = '3.3.6'
        guavaVersion = '19.0'
        protobufVersion = '3.1.0'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.grails.org/grails/core' }
        maven { url 'https://dl.bintray.com/grails/plugins' }
        maven { url 'https://repo.transmartfoundation.org/content/repositories/public' }
        maven { url 'https://repo.thehyve.nl/content/repositories/public' }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.google.guava:guava:$guavaVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
        classpath 'org.grails.plugins:hibernate5:6.0.4'
        classpath "com.google.protobuf:protobuf-java:${protobufVersion}"
        classpath "com.google.protobuf:protobuf-java-util:${protobufVersion}"
        classpath 'org.grails.plugins:views-gradle:1.0.12'
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath 'org.owasp:dependency-check-gradle:3.2.1'
    }
}

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.owasp.dependencycheck'

ext {
    transmartVersion = '17.1-HYVE-5-SNAPSHOT'

    aspectjVersion = '1.5.4'
    assetPipelineVersion = '2.14.1'
    gparsVersion = '1.2.0'
    gradleWrapperVersion = '4.8'
    grailsVersion = '3.3.6'
    guavaVersion = '19.0'
    jacksonVersion = '2.9.5'
    modelMapperVersion = '1.1.3'
    ojdb7Version = '12.1.0.1'
    openCsvVersion = '4.2'
    postgresqlVersion = '42.1.4'
    protobufVersion = '3.5.1'
    quartzVersion = '2.0.8'
    springSecurityPluginVersion = '3.2.1'
    springSecurityVersion = '4.2.7.RELEASE'
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

version = transmartVersion
group = 'org.transmartproject'

allprojects {
    ext {
        aspectjVersion = aspectjVersion
        assetPipelineVersion = assetPipelineVersion
        gparsVersion = gparsVersion
        guavaVersion = guavaVersion
        jacksonVersion = jacksonVersion
        modelMapperVersion = modelMapperVersion
        ojdb7Version = ojdb7Version
        openCsvVersion = openCsvVersion
        postgresqlVersion = postgresqlVersion
        protobufVersion = protobufVersion
        quartzVersion = quartzVersion
        springSecurityPluginVersion = springSecurityPluginVersion
        springSecurityVersion = springSecurityVersion
    }
    repositories {
        repositories {
            mavenLocal()
            mavenCentral()
            maven { url 'https://repo.grails.org/grails/core' }
            maven { url 'https://repo.transmartfoundation.org/content/repositories/public/' }
            maven { url 'https://repo.thehyve.nl/content/groups/public/' }
            maven { url 'https://dl.bintray.com/bluesliverx/grails-plugins' }
            //maven { url 'https://jcenter.bintray.com' }
        }
    }
}

subprojects { project ->
    version = transmartVersion
    group = 'org.transmartproject'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    if (project.name =~ /^(mock-ontology-server|transmart-core-db-tests|transmart-core-db|transmart-rest-api|transmart-notifications|transmart-api-server)$/) {
        if (project.name =~ /^(mock-ontology-server|transmart-api-server)$/) {
            apply plugin: 'war'
            apply plugin: 'org.grails.grails-web'
        } else {
            apply plugin: 'org.grails.grails-plugin'
            bootRepackage.enabled = false
        }
        apply plugin: 'org.grails.grails-gsp'
        apply plugin: 'asset-pipeline'

        dependencyManagement {
            imports {
                mavenBom "org.grails:grails-bom:$grailsVersion"
            }
            applyMavenExclusions false
        }
    }
    if (project.name =~ /^(transmart-core-api|transmart-data|transmart-batch|transmart-copy|transmart-api-server)$/) {
        apply plugin: 'net.linguica.maven-settings'
        apply plugin: 'maven-publish'
        apply plugin: 'org.owasp.dependencycheck'

        publishing {
            repositories {
                maven {
                    name "nl.thehyve.nexus"
                    url "https://repo.thehyve.nl/content/repositories/snapshots/"
                }
            }
        }
    }
}

